apply plugin: 'com.android.library'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode gitVersionCode()
        versionName gitVersionName()
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

static def gitVersionCode() {
    def process = "git rev-list --count HEAD".execute()
    return process.text.toInteger()
}

static def gitVersionName() {
    def process = "git describe --tags".execute()
    // Remove trailing CR and remove leading 'v' as in 'v1.2.3'
    return process.text.trim().substring(1)
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    api 'com.fasterxml.jackson.core:jackson-databind:2.9.6'
    api 'com.fasterxml.jackson.core:jackson-core:2.9.6'
    api 'org.java-websocket:Java-WebSocket:1.3.9'
}
